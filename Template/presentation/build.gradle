import static org.apache.tools.ant.taskdefs.condition.Os.FAMILY_WINDOWS
import static org.apache.tools.ant.taskdefs.condition.Os.isFamily

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
//apply from: '../buildsystem/lint-summary.gradle'

def keyConfigPath
if (isFamily(FAMILY_WINDOWS)) {
    keyConfigPath = System.getenv('USERPROFILE') + File.separator + ".ssh" + File.separator +
            "TemplateKeyStore.properties"
} else {
    keyConfigPath = System.getenv('HOME') + File.separator + ".ssh" + File.separator +
            "TemplateKeyStore.properties"
}

Properties props = new Properties()
if (new File(keyConfigPath).exists()) {
    props.load(new FileInputStream(file(keyConfigPath)))
}


android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        // Required for now!
        xmlOutput file("$buildDir/outputs/lint-results.xml")
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    signingConfigs {
        release {
            storeFile file(props['keystore'])
            storePassword props['keystore.password']
            keyAlias props['key.alias']
            keyPassword props['key.password']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.release
        }

        release {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors.whenObjectAdded { flavor ->
        def flavorData = rootProject.ext[flavor.name]
        flavor.applicationId flavorData.packageName
        flavor.versionCode flavorData.versionCode
        flavor.versionName flavorData.versionName
    }

    productFlavors {
        develop {}

        product {}
    }
}

dependencies {
    def presentationDependencies = rootProject.ext.presentationDependencies

    compile project(':model')
    compile presentationDependencies.androidSupportAppCompatV7
    compile presentationDependencies.butterKnife
    compile presentationDependencies.rxAndroid

    apt presentationDependencies.daggerCompiler
    provided presentationDependencies.javaxAnnotation

    compile presentationDependencies.mvp

    // UI library
    compile presentationDependencies.recyclerViewV7
    compile presentationDependencies.cardViewV7
    compile presentationDependencies.fresco
    compile presentationDependencies.loadToast

    // Developer
    developCompile presentationDependencies.xLogAndroid
    productCompile presentationDependencies.xLogAndroidIdle
    retrolambdaConfig presentationDependencies.retrolambda
    compile presentationDependencies.stetho
    compile(presentationDependencies.fabric) {
        transitive = true;
    }
    developCompile presentationDependencies.leakCanary
    productCompile presentationDependencies.leakCanaryIdle
}
